PREFIX nn:   <http://www.swows.org/xml/no-namespace#>
PREFIX svg:  <http://www.w3.org/2000/svg#>
PREFIX html: <http://www.w3.org/1999/xhtml#>
PREFIX p: <http://dev.w3.org/html5/2dcontext/#>
PREFIX sf: <http://www.swows.org/function#>
PREFIX swi: <http://www.swows.org/instance#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX : <http://www.swows.org/svg2canvas#>

CONSTRUCT {

  html:canvas
    nn:width ?svgWidth ;
    nn:height ?svgHeight ;
    p:contextScript ?script1 .

  ?script1
    a p:scale ;
    p:x ?viewBox_scaleX ;
    p:y ?viewBox_scaleY ;
    p:next ?script2 .

  ?script2
    a p:translate ;
    p:x ?viewBox_translateX ;
    p:y ?viewBox_translateY .

} WHERE {

  ?svg
    a svg:svg ;
#    nn:x ?svgX ;
#    nn:y ?svgY ;
    nn:width ?svgWidth ;
    nn:height ?svgHeight ;
    nn:viewBox ?viewBox .

  BIND( sf:analyze-string(?viewBox, "\\S+") AS ?viewBox_parsed ) .
  GRAPH ?viewBox_parsed {
    swi:GraphRoot sf:first-match ?match1 .
    ?match1
      sf:string ?viewBox_minX ;
      sf:next-match ?match2 .
    ?match2
      sf:string ?viewBox_minY ;
      sf:next-match ?match3 .
    ?match3
      sf:string ?viewBox_width ;
      sf:next-match ?match4 .
    ?match4
      sf:string ?viewBox_height .
  } .
  BIND( xsd:decimal(?viewBox_width) AS ?viewBox_scaleX ) .
  BIND( xsd:decimal(?viewBox_height) AS ?viewBox_scaleY ) .
  BIND( -1 * xsd:decimal(?viewBox_minX) AS ?viewBox_translateX ) .
  BIND( -1 * xsd:decimal(?viewBox_minY) AS ?viewBox_translateY ) .

  BIND( URI( CONCAT( STR(:script1_), MD5(STR(?svg)) ) ) AS ?script1 ) .
  BIND( URI( CONCAT( STR(:script2_), MD5(STR(?svg)) ) ) AS ?script2 ) .

}

